import com.pubnub.components.buildsrc.Libs
import com.pubnub.components.buildsrc.Urls
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext {
        compose_version = '1.1.0-beta04'
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath(Libs.androidGradlePlugin)
        classpath(Libs.Kotlin.gradlePlugin)
        classpath(Libs.Hilt.gradlePlugin)
        classpath(Libs.gradleMavenPublishPlugin)
        classpath(Libs.gradleVersionsPlugin)
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()

        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url Urls.composeSnapshotRepo }
            maven { url Urls.mavenCentralSnapshotRepo }
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors
            allWarningsAsErrors = true

            jvmTarget = "1.8"

            // Use experimental APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-Xopt-in=kotlin.time.ExperimentalTime'
        }
    }

    // androidx.test and hilt are forcing JUnit, 4.12. This forces them to use 4.13
    configurations.configureEach {
        resolutionStrategy {
            force(Libs.JUnit.junit)
            // jackson use old reflect 1.3
            force(Libs.Kotlin.reflect)
            dependencySubstitution {
                substitute module('org.hamcrest:hamcrest-core:1.1') with module('junit:junit:4.10')
            }
        }
    }
}

